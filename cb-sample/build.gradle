group 'com.matejhrazdira.cbsample'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

sourceSets {
    main {
        java {
            srcDirs += "src/main/java-cb"
        }
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile 'com.google.code.gson:gson:2.8.0'
}

task sampleJavah(type: Exec) {
    commandLine "javah", "-classpath", "./build/classes/main", "-d", "./src/main/cpp", "-verbose", "com.matejhrazdira.cbsample.Sample"
}

sampleJavah {
    dependsOn classes
}

task corbaBinding(type: Exec) {
    inputs.files fileTree(dir: 'src/main/idl', include: '**/*.idl')
    def cbJava = fileTree(dir: 'src/main/java-cb', include: '**/*.java')
    def cbCpp = fileTree(dir: 'src/main/cpp-cb', include: '**/*.*')
    outputs.files cbJava + cbCpp
    workingDir 'src/main'
    commandLine "java", "-jar", "../../../build/libs/corbabinding-0.05.000.jar", "idl/Config.json"
}

task corbaBindingForce(type: Exec) {
    workingDir 'src/main'
    commandLine "java", "-jar", "../../../build/libs/corbabinding-0.05.000.jar", "idl/Config.json"
}

task corbaBindingClean(type: Delete) {
    delete "src/main/java-cb", "src/main/cpp-cb"
}

task cmake(type: Exec) {
    commandLine "cmake", "."
}

task cmakeEclipse(type: Exec) {
    commandLine "cmake", "-GEclipse CDT4 - Unix Makefiles", "-DCMAKE_BUILD_TYPE=Debug", "."
}

task make(type: Exec) {
    commandLine "make", "all", "-j10"
}

task makeClean(type: Exec) {
    commandLine "make", "clean"
}

cmakeEclipse {
    dependsOn corbaBinding
}

make {
    dependsOn cmakeEclipse
}

makeClean {
    dependsOn cmakeEclipse
}

compileJava {
    dependsOn corbaBinding
}

assemble {
    dependsOn make
}

clean {
    dependsOn makeClean, corbaBindingClean
}
